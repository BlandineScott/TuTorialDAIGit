Set TestList to ["Contacts", "Downloads", "History"] -- Creates a series of script executions to be used later
RunWithNewResults "Connection"
set Outcome to the result
if the status of Outcome is not "Success" -- If the result of "InitialTest" is not "Success", gets the date and time of the run...
then
	convert Outcome's runDate to date and long time
	sendMail {to: "blandine.scott@keysight.com", from: "blandine.fromontscott@gmail.com", subject: "Initial Test Failed", body: "Test run at" && rundate of Outcome && "had" && errors of Outcome && "errors"} -- Sends email to report the date and errors of the execution. (&& joins text strings with a space between them.)
else -- Otherwise...
	repeat with each testScript of TestList -- For every script in TestList,
		RunWithNewResults testScript -- Runs the script, then...
		set Outcome to the result -- Puts the results into Outcome
		put testScript & ":" && status of Outcome && Return after currentReport -- Adds "Script: Status", then a return character to currentReport
		if the status of Outcome is "Failure" then -- If the status property is Failure...
			run "EndScript" -- Runs CleanupScript to reset the test environment
		end if
	end repeat -- Ends after the final test in TestList
	Log "Final Results:" -- Logs "Final Results:" text
	repeat with each line of currentReport
		log it -- Logs each line in currentReport
	end repeat
end if